ACTOR NewShotgunGuy Replaces ShotgunGuy
{
  Health 60
  Radius 20
  Height 56
  Mass 100
  Speed 12
  PainChance 170
  PainChance "Disarm", 255
  PainChance "CustomMelee", 255
  PainChance "Knockback", 255
  Monster
  +FLOORCLIP
  SeeSound "shotguy/sight"
  AttackSound "shotguy/attack"
  PainSound "shotguy/pain"
  DeathSound "shotguy/death"
  ActiveSound "shotguy/active"
  Obituary "$OB_SHOTGUY"
  DropItem "NewShell"
  States
  {
  Spawn:
    SPOS AB 10 A_Look
    Loop
  See:
    SPOS AABBCCDD 3 A_Chase
    Loop
  Missile:
    SPOS E 10 A_FaceTarget
	SPOS F 0 bright A_PlaySound("shotguy/attack", CHAN_WEAPON)
	SPOS F 0 bright A_CustomMissile("ZombieBullet2", 28, 0, Random(-18, 18), 0, 0)
	SPOS F 0 bright A_CustomMissile("ZombieBullet2", 28, 0, Random(-18, 18), 0, 0)
	SPOS F 0 bright A_CustomMissile("ZombieBullet2", 28, 0, Random(-18, 18), 0, 0)
	SPOS F 0 bright A_CustomMissile("ZombieBullet2", 28, 0, Random(-18, 18), 0, 0)
	SPOS F 0 bright A_CustomMissile("ZombieBullet2", 28, 0, Random(-18, 18), 0, 0)
	SPOS F 10 bright A_CustomMissile("ZombieBullet2", 28, 0, Random(-18, 18), 0, 0)
    SPOS E 10
    Goto See
  Pain:
    SPOS G 3
    SPOS G 3 A_Pain
    Goto See
  Pain.Disarm:
	TNT1 A 0 A_ChangeFlag("Invulnerable", true)
	SPOS G 6
    SPOS G 6 A_Pain
	TNT1 A 0 A_GiveToTarget("ShotgunGiver", 1)
	TNT1 A 0 A_ChangeFlag("NoBlockMap", true)
    Goto Death
  Death:
    SPOS H 5
    SPOS I 5 A_Scream
    SPOS J 5 A_NoBlocking
    SPOS K 5
    SPOS L -1
    Stop
  XDeath:
    SPOS M 5
    SPOS N 5 A_XScream
    SPOS O 5 A_NoBlocking
    SPOS PQRST 5
    SPOS U -1
    Stop
  Raise:
    SPOS L 5
    SPOS KJIH 5
    Goto See
  }
}

Actor ShotgunGiver: CustomInventory
//The actor you get to get given the shotgun for disarming a zombie.
{
	+INVENTORY.AUTOACTIVATE
	States
	{
		Use:
		/* 
		*  All of the pickup failing isn't strictly necessary, 
		*  but I don't want to deal with what happens in the
		*  case that you're running over a weapon pickup as 
		*  you disarm, or any other silly edge cases.
		*/
		TNT1 A 0 A_JumpIfInventory("CombatShotgun", 1, "PickupFail")
		TNT1 A 0 A_JumpIfInventory("NewChaingun", 1, "PickupFail")
		TNT1 A 0 A_JumpIfInventory("NewRocketLauncher", 1, "PickupFail")
		TNT1 A 0 A_JumpIfInventory("NewSSG", 1, "PickupFail")
		TNT1 A 0 A_JumpIfInventory("NewPlasmaGun", 1, "PickupFail")
		TNT1 A 0 A_PlaySound("misc/w_pkup")
		TNT1 A 0 A_GiveInventory("CombatShotgun")
		TNT1 A 0 A_TakeInventory("PidgeonPuncher", 1)
		TNT1 A 0 ACS_ExecuteAlways(655)
		Stop
		PickupFail:
		TNT1 A 0 A_SpawnItemEx("ShotgunPickup", 64, 0, 18, 18, 0, 0, 0)
	}
}