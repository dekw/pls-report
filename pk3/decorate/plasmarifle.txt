Actor NewPlasmaGun : Weapon Replaces PlasmaRifle
{
	Inventory.PickupMessage "You got the Plasma Rifle!"
	Inventory.pickupsound "misc/w_pkup"
	Weapon.Ammotype1 "LoadedPlasma"	
	Weapon.Ammouse1 1
	Weapon.Ammotype2 "NewCell"
	Weapon.Ammogive2 0
	Weapon.Kickback 25
	
	+INVENTORY.UNDROPPABLE
	+WEAPON.AMMO_OPTIONAL
	States
	{
		Spawn:
			D3PG A -1
			Loop
		Ready:
			TNT1 A 0 A_JumpIfInventory("ReloadChecker", 1, "ReloadStart")
			TNT1 A 0 A_JumpIfInventory("DropChecker", 1, "Drop")
			NWPG A 1 A_WeaponReady
			Loop
		Deselect:
			NWPG A 1 A_Lower
			TNT1 A 0 A_Lower
			NWPG A 1 A_Lower
			TNT1 A 0 A_Lower
			NWPG A 1 A_Lower
			TNT1 A 0 A_Lower
			NWPG A 1 A_Lower
			TNT1 A 0 A_Lower
			NWPG A 1 A_Lower
			TNT1 A 0 A_Lower
			NWPG A 1 A_Lower
			TNT1 A 0 A_Lower
			TNT1 A 0 ACS_ExecuteAlways(61)
			Loop
		Drop:
			TNT1 A 0 A_TakeInventory("DropChecker", 1)
			SHTG A 1 ACS_ExecuteAlways(651)
			TNT1 A 0 A_GiveInventory("PidgeonPuncher", 1)
			TNT1 A 0 A_SpawnItemEx("PGPickup", 64, 0, 18, 18, 0, 0, 0)
			//trying sloppily to make spamming drop/reload not break stuff
			TNT1 A 0 A_TakeInventory("DropChecker", 1)
			TNT1 A 0 A_TakeInventory("NewPlasmaGun", 1)
			//Give dual fists back when you have no heavy weapon.
			TNT1 A 0 A_GiveInventory("PidgeonPuncher")
			Goto Ready
		Select:
			TNT1 A 0 ACS_ExecuteAlways(651) //see: feeling better
			TNT1 A 0 ACS_ExecuteAlways(654)
			NWPG A 1 A_Raise
			TNT1 A 0 A_Raise
			Loop
		Fire:
			TNT1 A 0 A_JumpIfNoAmmo("ReloadStart")
			NWPF A 1 A_FireCustomMissile("PlasmaShot",0,1,-2,-3)
			TNT1 A 0 A_PlaySound("sounds/plasmafire")
			TNT1 A 0 A_FireCustomMissile("PlasmaTrailSpawner",0,0,-2,-3)
			NWPF B 1
			NWPF C 1
			NWPF C 1 Offset(-6,38)
			NWPF C 1 Offset(-9,43)
			NWPF C 1 Offset(-12,48)
			NWPF D 6 Offset(-12,48)
			NWPF C 1 Offset(-12,48)
			NWPF C 1 Offset(-11,45)
			NWPF C 1 Offset(-10,42)
			NWPF C 1 Offset(-8,40)
			NWPF C 1 Offset(-6,38)
			NWPF C 1 Offset(-4,36)
			NWPF C 1 Offset(-2,34)
			NWPF C 1 Offset(0,32)
			NWPF B 2
			Goto Ready
		AltFire:
			NWPF A 1 Offset(-16, 54)
			NWPF A 1 Offset(-32, 68)
			RPUN B 1 Offset(0, 32)
			RPUN C 2 A_CustomPunch(40, 1, 4, "KnockbackPuff")
			RPUN D 8
			RPUN C 7
			Goto Ready
		ReloadStart:
			TNT1 A 0 A_TakeInventory("ReloadChecker", 1)
			TNT1 A 0 A_JumpIfInventory("LoadedPlasma", 0, "ReloadFail")
			//0 meaning max for JumpIfInventory
			TNT1 A 0 A_JumpIfInventory("NewCell", 1, "ReloadStart2")
			Goto ReloadFail
		ReloadStart2:
			/*Offsets are cancelled without WRF_NOBOB
			* WRF_NOPRIMARY so you can punch when you want
			* along with A_WeaponReady allowing you to altfire
			**/
			NWPG A 1 Offset(0, 32) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			NWPG A 1 Offset(0, 34) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			NWPG A 1 Offset(0, 36) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			NWPG A 1 Offset(0, 38) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			NWPG A 1 Offset(0, 40) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			NWPG A 1 Offset(0, 42) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			NWPG A 1 Offset(0, 44) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			NWPG A 1 Offset(0, 46) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			NWPG A 1 Offset(0, 48) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			NWPG A 1 Offset(0, 50) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			NWPG A 1 Offset(0, 52) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			NWPG A 1 Offset(0, 54) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			NWPG A 1 Offset(0, 56) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			NWPG A 1 Offset(0, 58) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			NWPG A 1 Offset(0, 60) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			NWPG A 2 Offset(0, 62) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			NWPG A 0 Offset(0, 64) A_PlaySound("sounds/pistolload", CHAN_WEAPON)
			NWPG A 18 Offset(0, 64) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			Goto ReloadLoop
		ReloadLoop:
			TNT1 A 0 A_TakeInventory("NewCell", 1)
			TNT1 A 0 A_GiveInventory("LoadedPlasma", 1)
			TNT1 A 0 A_JumpIfInventory("LoadedPlasma", 0, "ReloadEnd")
			TNT1 A 0 A_JumpIfInventory("NewCell", 1, "ReloadLoop")
		ReloadFail:
			NWPG A 1
			Goto Ready
		ReloadEnd:
			NWPG A 2 Offset(0, 62) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			NWPG A 2 Offset(0, 59) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			NWPG A 2 Offset(0, 56) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			NWPG A 2 Offset(0, 53) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			NWPG A 2 Offset(0, 50) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			NWPG A 2 Offset(0, 47) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			NWPG A 2 Offset(0, 44) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			NWPG A 2 Offset(0, 41) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			NWPG A 2 Offset(0, 38) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			NWPG A 2 Offset(0, 35) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			NWPG A 2 Offset(0, 32) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			Goto Ready
	}
}

Actor PlasmaShot: FastProjectile
{
   Radius 8
   Height 12
   Speed 30
   Damage (75)
   PROJECTILE
   scale 0.58
   MissileHeight 64
   DeathSound "sounds/plasmahit"
   DONTHURTSHOOTER
   Decal RevenantScorch
   States
   {
   Spawn: 
      BLST A 1 Bright 
	  BLST A 1 Bright
	  BLST A 1 Bright
	  BLST A 1 Bright
	  BLST A 1 Bright
	  BLST A 1 Bright
	  BLST A 2 Bright A_ScaleVelocity(1.5)
	  BLST A 2 Bright A_ScaleVelocity(1.5)
	  BLST A 1 Bright A_ScaleVelocity(1.25)
      Goto Spawn2
   Spawn2:
	  BLST A 1 Bright
	  Loop
   Death:
      BLST B 6 Bright
	  TNT1 AAAAAA 0 A_CustomMissile("Spark_O1", (Random(-2, 2)), 0, (Random(-60, 60)), 0, 0)
      BLST CDEFGHIJKL 3 Bright 
      stop
   }
}

Actor PlasmaTrailSpawner
{
	Speed 30
	PROJECTILE
	Damage 0
	States
	{
		Spawn: 
		  TNT1 A 1 A_SpawnItemEx("PlasmaSpark", 0, 0, 0, Random(0,2), Random(0,2), Random(0,2), Random(0, 359), 0)
		  TNT1 A 1 A_SpawnItemEx("PlasmaSpark", 0, 0, 0, Random(0,2), Random(0,2), Random(0,2), Random(0, 359), 0)
		  TNT1 A 1 A_SpawnItemEx("PlasmaSpark", 0, 0, 0, Random(0,2), Random(0,2), Random(0,2), Random(0, 359), 0)
		  TNT1 A 1 A_SpawnItemEx("PlasmaSpark", 0, 0, 0, Random(0,2), Random(0,2), Random(0,2), Random(0, 359), 0)
		  TNT1 A 1 A_SpawnItemEx("PlasmaSpark", 0, 0, 0, Random(0,2), Random(0,2), Random(0,2), Random(0, 359), 0)
		  TNT1 A 1 A_SpawnItemEx("PlasmaSpark", 0, 0, 0, Random(0,2), Random(0,2), Random(0,2), Random(0, 359), 0)
		  TNT1 A 2 A_ScaleVelocity(1.5)
		  TNT1 A 0 A_SpawnItemEx("PlasmaSpark", 0, 0, 0, Random(0,2), Random(0,2), Random(0,2), Random(0, 359), 0)
		  TNT1 A 2 A_ScaleVelocity(1.5)
		  TNT1 A 0 A_SpawnItemEx("PlasmaSpark", 0, 0, 0, Random(0,2), Random(0,2), Random(0,2), Random(0, 359), 0)
		  TNT1 A 1 A_ScaleVelocity(1.25)
		  TNT1 A 0 A_SpawnItemEx("PlasmaSpark", 0, 0, 0, Random(0,2), Random(0,2), Random(0,2), Random(0, 359), 0)
		  Goto Spawn2
		Spawn2:
		  TNT1 A 1 Bright A_SpawnItemEx("PlasmaSpark", 0, 0, 0, Random(0,5), Random(0,5), Random(0,5), Random(0, 359), 0)
		  Loop
		Death:
		  TNT1 A 1
		  Stop
	}
}

Actor PlasmaSpark: Spark_O1
{	
	+NoGravity
	+NoInteraction
	Mass 1
	Speed 0
	-Missile
	States
	{
		Spawn:
		SPKO EEEEEEEEEEEE 1 bright A_FadeOut(0.1)
		Stop
	}
}

Actor LoadedPlasma : Ammo
{
	Inventory.Amount 1
	Inventory.MaxAmount 16
	Ammo.BackpackAmount 0
	Ammo.BackPackMaxAmount 16
}

Actor PGPickup: CustomInventory Replaces PlasmaRifle
{
	Inventory.PickupMessage "You got the Plasma Rifle!"
	States
	{
		Spawn:
		PLAS A -1
		Stop
		
		Pickup:
		TNT1 A 0 A_JumpIfInventory("CombatShotgun", 1, "PickupFail")
		TNT1 A 0 A_JumpIfInventory("NewChaingun", 1, "PickupFail")
		TNT1 A 0 A_JumpIfInventory("NewRocketLauncher", 1, "PickupFail")
		TNT1 A 0 A_JumpIfInventory("NewSSG", 1, "PickupFail")
		TNT1 A 0 A_JumpIfInventory("NewPlasmaGun", 1, "PickupFail")
		TNT1 A 0 A_GiveInventory("NewPlasmaGun")
		//Take unarmed away with heavy weapon, give it back without.
		TNT1 A 0 A_TakeInventory("PidgeonPuncher", 1)
		Stop
		
		PickupFail:
		PLAS A 1
		Fail
	}
}


Actor PGPickup2: PGPickup Replaces NewPlasmaGun{} 

ACTOR NewCell : Ammo Replaces Cell
{
  Inventory.PickupMessage "$GOTCELL" // "Picked up an energy cell."
  Inventory.Amount 16
  Inventory.MaxAmount 64
  Ammo.BackpackAmount 16
  Ammo.BackpackMaxAmount 80
  Inventory.Icon "CELLA0"
  States
  {
  Spawn:
    CELL A -1
    Stop
  }
}

ACTOR NewCellPack : NewCell Replaces CellPack
{
  Inventory.PickupMessage "$GOTCELLBOX" // "Picked up an energy cell pack."
  Inventory.Amount 32
  States
  {
  Spawn:
    CELP A -1
    Stop
  }
}

ACTOR NewBFG: BFG9000 Replaces BFG9000
{
	Weapon.AmmoType "NewCell"
	Weapon.AmmoUse 5
	Weapon.AmmoGive 5
}