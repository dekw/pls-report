Actor NewSSG : Weapon Replaces SuperShotgun
//This gun is meant to be over-the-top -- ergo recoil and all that stuff
{
	Weapon.AmmoUse1 2
	Weapon.AmmoGive 0
	Weapon.AmmoType1 "LoadedSSG"
	Weapon.Ammotype2 "NewShell"
	Inventory.PickupMessage "You got the 'Piecemaker' shotgun!"
	Weapon.Kickback 500 //HEY, YOU *CAN* SET KNOCKBACK!
	
	+INVENTORY.UNDROPPABLE
	+WEAPON.NOAUTOAIM
	+WEAPON.AMMO_OPTIONAL
	
	States
	{
		Ready:
			TNT1 A 0 A_JumpIfInventory("ReloadChecker", 1, "ReloadStart")
			SHT2 A 1 A_WeaponReady
			TNT1 A 0 A_JumpIfInventory("DropChecker", 1, "Drop")
			Loop
		Deselect:
			TNT1 A 0 A_TakeInventory("DropChecker", 1) //swiper no reloading
			SHT2 A 2 A_Lower
			SHT2 A 2 A_Lower
			SHT2 A 2 A_Lower
			SHT2 A 2 A_Lower
			SHT2 A 2 A_Lower
			TNT1 A 0 A_TakeInventory("DropChecker", 1)
			TNT1 A 0 ACS_ExecuteAlways(651)
			Loop
		Drop:
			TNT1 A 0 A_TakeInventory("DropChecker", 1)
			CHGG A 1 ACS_ExecuteAlways(651)
			TNT1 A 0 A_GiveInventory("PidgeonPuncher", 1)
			TNT1 A 0 A_SpawnItemEx("SSGPickup", 64, 0, 18, 18, 0, 0, 0)
			TNT1 A 0 A_TakeInventory("DropChecker", 1)
			//Give dual fists back when you have no heavy weapon.
			TNT1 A 0 ACS_ExecuteAlways(660) //switch to unarmed.
			TNT1 A 0 A_TakeInventory("NewSSG", 1)
			Goto Ready
		Select:
			TNT1 A 0 ACS_ExecuteAlways(651) //redundant, but makes me feel better
			TNT1 A 0 A_TakeInventory("ReloadChecker", 1) 
			TNT1 A 0 ACS_ExecuteAlways(653)
			SHT2 A 2 A_Raise
			Loop
		Fire:
		    TNT1 A 0 A_JumpIfNoAmmo("ReloadStart")
			SHT2 A 3
			//38 Pellets inc -- YES I KNOW I OBSESSIVELY MAKE 0 TIC FRAMES TNT1 SHUT UP
			TNT1 A 0 A_PlaySound ("weapons/sshotf", CHAN_WEAPON)
			TNT1 A 0 A_PlaySound ("weapons/rocklx", CHAN_WEAPON)
			TNT1 A 0 Radius_Quake(1, 35, 0, 32, 0)
			TNT1 AAAAAAAAAAAAA 0 A_FireCustomMissile("Spark_O1", frandom(-45, 45), 0, 0, 8, 0, frandom(-24, 24))
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_FireCustomMissile("SSGPellet", frandom(-15, 15), 0, 0, 0, 0, frandom(-8, 8))
			TNT1 A 0 A_FireCustomMissile("SSGPellet", frandom(-15, 15), 1, 0, 0, 0, frandom(-8, 8))
			SHT2 A 7 A_Recoil(25.0)
			SHT2 B 3
			SHT2 C 29 A_WeaponReady(WRF_NOPRIMARY)
			Goto ReloadStart
		ReloadStart:
			TNT1 A 0 A_TakeInventory("ReloadChecker", 1)
			TNT1 A 0 A_JumpIfInventory("NewShell", 1, "ReloadLoop")
			Goto ReloadFail
		ReloadLoop: 
			TNT1 A 0 A_OpenShotgun2
			SHT2 D 6 A_WeaponReady(WRF_NOPRIMARY)
			SHT2 E 4 A_WeaponReady(WRF_NOPRIMARY)
			TNT1 F 0 A_LoadShotgun2
			SHT2 F 5 A_WeaponReady(WRF_NOPRIMARY)
			SHT2 G 7 A_WeaponReady(WRF_NOPRIMARY)
			TNT1 A 0 A_TakeInventory("NewShell", 1)
			TNT1 A 0 A_GiveInventory("LoadedSSG", 1)
			TNT1 A 0 A_CloseShotgun2
			SHT2 H 8 A_WeaponReady(WRF_NOPRIMARY)
			TNT1 A 0 A_JumpIfInventory("LoadedSSG", 0, "ReloadEnd")
			SHT2 B 3 A_WeaponReady(WRF_NOPRIMARY)
			SHT2 C 4 A_WeaponReady(WRF_NOPRIMARY)
			TNT1 A 0 A_JumpIfInventory("NewShell", 1, "ReloadLoop")
		ReloadEnd:
			SHT2 A 5
			Goto Ready
		ReloadFail:
			SHT2 A 1
			Goto Ready
		
		AltFire:
			SHT2 A 1 Offset(-16, 54)
			SHT2 A 1 Offset(-32, 68)
			RPUN B 1 Offset(0, 32)
			RPUN C 2 A_CustomPunch(40, 1, 4, "KnockbackPuff")
			RPUN D 8
			RPUN C 7
			Goto Ready
		Flash:
			SHT2 I 4 Bright A_Light1
			SHT2 J 3 Bright A_Light2
			Goto LightDone
		Spawn:
			SGN2 A -1
			Stop
	}
}

Actor SSGPellet: PistolBullet
{	
	Scale 0.2
	+FORCERADIUSDMG
	+EXTREMEDEATH
	Damage (14)
	Speed 48
	Decal bulletChip1
	States
	{
		Death:
			TNT1 A 0 A_Explode(8, 32)
			MISL ABC 4 A_CustomMissile("Spark_O1", (Random(-2, 2)), 0, (Random(-60, 60)), 0, 0)
			Stop
	}
}

Actor SSGpickup: CustomInventory Replaces SuperShotgun
{
	Inventory.PickupMessage "You got the 'Piecemaker' shotgun!"
	Inventory.PickupSound "misc/w_pkup"
	States
	{
		Spawn:
		SGN2 A -1
		Stop
		
		Pickup:
		TNT1 A 0 A_JumpIfInventory("CombatShotgun", 1, "PickupFail")
		TNT1 A 0 A_JumpIfInventory("NewChaingun", 1, "PickupFail")
		TNT1 A 0 A_JumpIfInventory("NewRocketLauncher", 1, "PickupFail")
		TNT1 A 0 A_JumpIfInventory("NewSSG", 1, "PickupFail")
		TNT1 A 0 A_JumpIfInventory("NewPlasmaGun", 1, "PickupFail")
		TNT1 A 0 A_GiveInventory("NewSSG")
		TNT1 A 0 ACS_ExecuteAlways(657) //Switch to acquired weapon
		//Take unarmed away with heavy weapon, give it back without.
		TNT1 A 0 A_TakeInventory("PidgeonPuncher", 1)
		Stop
		
		PickupFail:
		SGN2 A 1
		Fail
	}
}

Actor SSGPickup2: SSGPickup Replaces NewSSG {} 

Actor LoadedSSG : Ammo
{
	Inventory.Amount 2
	Inventory.MaxAmount 2 
	Ammo.BackpackAmount 0
	Ammo.BackPackMaxAmount 2
}