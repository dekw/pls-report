Actor NewSSG : Weapon Replaces SuperShotgun
//This gun is meant to be over-the-top -- ergo recoil and all that stuff
{
	Weapon.AmmoUse 4
	Weapon.AmmoGive 0
	Weapon.AmmoType "NewShell"
	Inventory.PickupMessage "You got the 'Piecemaker' shotgun!"
	Weapon.Kickback 800 //HEY, YOU *CAN* SET KNOCKBACK!
	
	+INVENTORY.UNDROPPABLE
	+WEAPON.NOAUTOAIM

	
	States
	{
		Ready:
			SHT2 A 1 A_WeaponReady
			TNT1 A 0 A_JumpIfInventory("DropChecker", 1, "Drop")
			Loop
		Deselect:
			SHT2 A 2 A_Lower
			SHT2 A 2 A_Lower
			SHT2 A 2 A_Lower
			SHT2 A 2 A_Lower
			SHT2 A 2 A_Lower
			TNT1 A 0 ACS_ExecuteAlways(651)
			Loop
		Drop:
			TNT1 A 0 A_TakeInventory("DropChecker", 1)
			CHGG A 1 ACS_ExecuteAlways(651)
			TNT1 A 0 A_GiveInventory("PidgeonPuncher", 1)
			TNT1 A 0 A_SpawnItemEx("SSGPickup", 64, 0, 18, 18, 0, 0, 0)
			TNT1 A 0 A_TakeInventory("DropChecker", 1)
			TNT1 A 0 A_TakeInventory("NewSSG", 1)
			//Give dual fists back when you have no heavy weapon.
			TNT1 A 0 A_GiveInventory("PidgeonPuncher")
			Goto Ready
		Select:
			TNT1 A 0 ACS_ExecuteAlways(651) //redundant, but makes me feel better
			TNT1 A 0 A_TakeInventory("ReloadChecker", 1) 
			TNT1 A 0 ACS_ExecuteAlways(653)
			SHT2 A 2 A_Raise
			Loop
		Fire:
			SHT2 A 3
			//38 Pellets inc -- YES I KNOW I OBSESSIVELY MAKE 0 TIC FRAMES TNT1 SHUT UP
			TNT1 A 0 A_PlaySound ("weapons/sshotf", CHAN_WEAPON)
			TNT1 A 0 A_PlaySound ("weapons/rocklx", CHAN_WEAPON)
			TNT1 A 0 Radius_Quake(1, 35, 0, 32, 0)
			TNT1 AAAAAAAAAAAAA 0 A_FireCustomMissile("Spark_O1", (random(-4500, 4500)/100.0), 0, 0, 8, 0, (random(-2400, 2400)/100.0))
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_FireCustomMissile("SSGPellet", (random(-1500, 1500)/100.0), 0, 0, 0, 0, (random(-800, 800)/100.0))
			TNT1 A 0 A_FireCustomMissile("SSGPellet", (random(-1500, 1500)/100.0), 1, 0, 0, 0, (random(-800, 800)/100.0))
			SHT2 A 7 A_Recoil(25.0)
			SHT2 B 3
			SHT2 C 29 A_CheckReload
			SHT2 D 6 A_OpenShotgun2
			SHT2 E 4
			SHT2 F 5 A_LoadShotgun2
			SHT2 G 7
			SHT2 H 8 A_CloseShotgun2
			SHT2 B 3
			SHT2 C 4
			SHT2 D 5 A_OpenShotgun2
			SHT2 E 4
			SHT2 F 5 A_LoadShotgun2
			SHT2 G 6
			SHT2 H 8 A_CloseShotgun2
			SHT2 A 5 
			Goto Ready
		AltFire:
			SHT2 A 1 Offset(-16, 54)
			SHT2 A 1 Offset(-32, 68)
			RPUN B 1 Offset(0, 32)
			RPUN C 2 A_CustomPunch(40, 1, 4, "KnockbackPuff")
			RPUN D 8
			RPUN C 7
			Goto Ready
		Flash:
			SHT2 I 4 Bright A_Light1
			SHT2 J 3 Bright A_Light2
			Goto LightDone
		Spawn:
			SGN2 A -1
			Stop
	}
}

Actor SSGPellet: PistolBullet
{	
	Scale 0.2
	+FORCERADIUSDMG
	+EXTREMEDEATH
	Damage (14)
	Speed 48
	Decal bulletChip1
	States
	{
		Death:
			TNT1 A 0 A_Explode(8, 32)
			MISL ABC 4 A_CustomMissile("Spark_O1", (Random(-2, 2)), 0, (Random(-60, 60)), 0, 0)
			Stop
	}
}

Actor SSGpickup: CustomInventory Replaces SuperShotgun
{
	Inventory.PickupMessage "You got the 'Piecemaker' shotgun!"
	States
	{
		Spawn:
		SGN2 A -1
		Stop
		
		Pickup:
		TNT1 A 0 A_JumpIfInventory("CombatShotgun", 1, "PickupFail")
		TNT1 A 0 A_JumpIfInventory("NewChaingun", 1, "PickupFail")
		TNT1 A 0 A_JumpIfInventory("NewRocketLauncher", 1, "PickupFail")
		TNT1 A 0 A_JumpIfInventory("NewSSG", 1, "PickupFail")
		TNT1 A 0 A_JumpIfInventory("NewPlasmaGun", 1, "PickupFail")
		TNT1 A 0 A_GiveInventory("NewSSG")
		//Take unarmed away with heavy weapon, give it back without.
		TNT1 A 0 A_TakeInventory("PidgeonPuncher", 1)
		Stop
		
		PickupFail:
		SGN2 A 1
		Fail
	}
}

Actor SSGPickup2: SSGPickup Replaces NewSSG {} 
