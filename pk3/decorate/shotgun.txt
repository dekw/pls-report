Actor CombatShotgun : Weapon
//Practical shotgun: decent at everything, versatile. Doesn't pack a massive punch.
{
	Weapon.Ammotype1 "LoadedShell"	
	Weapon.Ammogive2 0
	Weapon.Ammouse1 1
	Weapon.Ammotype2 "NewShell"
	Inventory.PickupMessage "You got the anti-personnel combat shotgun!"
	DropItem "ShotgunPickup"
	+INVENTORY.UNDROPPABLE
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.NOAUTOAIM
	
	States
	{
		Ready:
			TNT1 A 0 A_JumpIfInventory("ReloadChecker", 1, "ReloadStart")
			TNT1 A 0 A_JumpIfInventory("DropChecker", 1, "Drop")
			SHTG A 1 A_WeaponReady
			Loop
		Drop:
			TNT1 A 0 A_TakeInventory("DropChecker", 1)
			SHTG A 1 ACS_ExecuteAlways(651)
			TNT1 A 0 A_GiveInventory("PidgeonPuncher", 1)
			TNT1 A 0 A_SpawnItemEx("ShotgunPickup", 64, 0, 18, 18, 0, 0, 0)
			//trying sloppily to make spamming drop/reload not break stuff
			TNT1 A 0 A_TakeInventory("DropChecker", 1)
			TNT1 A 0 ACS_ExecuteAlways(660) //switch to unarmed.
			TNT1 A 0 A_TakeInventory("CombatShotgun", 1)
			//Give dual fists back when you have no heavy weapon.
			Goto Ready
		Deselect:
			TNT1 A 0 A_Lower
			SHTG A 1 A_Lower
			Loop
		Select:
			TNT1 A 0 ACS_ExecuteAlways(651)
			TNT1 A 0 A_TakeInventory("ReloadChecker", 1) 
			TNT1 A 0 A_Raise
			SHTG A 1 A_Raise
			Loop
		Fire: 
			TNT1 A 0 A_JumpIfNoAmmo("ReloadStart")
			SHTG A 5 A_PlaySound("weapons/shotgf", CHAN_WEAPON)
			SHTG A 0 A_FireCustomMissile("ShotgunPellet", (random(-400, 400)/100.0), 1, 0, 0, 0, (random(-300, 300)/100.0))
			SHTG A 0 A_FireCustomMissile("ShotgunPellet", (random(-400, 400)/100.0), 0, 0, 0, 0, (random(-300, 300)/100.0))
			SHTG A 0 A_FireCustomMissile("ShotgunPellet", (random(-300, 300)/100.0), 0, 0, 0, 0, (random(-200, 200)/100.0))
			SHTG A 0 A_FireCustomMissile("ShotgunPellet", (random(-300, 300)/100.0), 0, 0, 0, 0, (random(-200, 200)/100.0))
			SHTG A 0 A_FireCustomMissile("ShotgunPellet", (random(-200, 200)/100.0), 0, 0, 0, 0, (random(-100, 100)/100.0))
			SHTG A 0 A_FireCustomMissile("ShotgunPellet", (random(-200, 200)/100.0), 0, 0, 0, 0, (random(-100, 100)/100.0))
			SHTG A 0 A_FireCustomMissile("ShotgunPellet", (random(-100, 100)/100.0), 0, 0, 0, 0, 0)
			SHTG B 2
			SHTG C 10
			SHTG DC 3
			SHTG B 2
			SHTG A 5 A_ReFire
			Goto Ready
		AltFire:
			SHTG A 1 Offset(-16, 54)
			SHTG A 1 Offset(-32, 68)
			RPUN B 1 Offset(0, 32)
			RPUN C 2 A_CustomPunch(40, 1, 4, "KnockbackPuff")
			RPUN D 8
			RPUN C 7
			Goto Ready
		Flash:
			SHTF A 3 Bright A_Light1
			SHTF B 2 Bright A_Light2
			Goto LightDone
		Spawn:
			SHOT A -1
			Stop
		ReloadStart:
			TNT1 A 0 A_TakeInventory("ReloadChecker", 1)
			TNT1 A 0 A_JumpIfInventory("NewShell", 1, "ReloadStart2")
			Goto ReloadFail
		ReloadStart2:
			SHTG ABC 4 A_WeaponReady(WRF_NOPRIMARY)
			Goto ReloadLoop
		ReloadLoop:
			SHTG D 5 A_WeaponReady(WRF_NOPRIMARY)
			SHTG C 7 A_WeaponReady(WRF_NOPRIMARY) //0 tic loads, 8 tics of punch readiness
			TNT1 A 0 A_TakeInventory("NewShell", 1)
			TNT1 A 0 A_GiveInventory("LoadedShell", 1)
			SHTG C 0 A_PlaySound("misc/w_pkup", CHAN_WEAPON)
			SHTG C 3 A_WeaponReady
			TNT1 A 0 A_JumpIfInventory("LoadedShell", 0, "ReloadEnd")
			TNT1 A 0 A_JumpIfInventory("NewShell", 1, "ReloadLoop")
		ReloadEnd:
			SHTG DCBA 4 A_WeaponReady(WRF_NOPRIMARY)
			Goto Ready
		ReloadFail:
			SHTG A 1
			Goto Ready
	}
}

Actor ShotgunPellet: PistolBullet
{
	Damage (12)
}

Actor LoadedShell : Ammo
{
	Inventory.Amount 1
	Inventory.MaxAmount 6 
	Ammo.BackpackAmount 0
	Ammo.BackPackMaxAmount 6
}

Actor ShotgunPickup: CustomInventory Replaces Shotgun
{
	Inventory.PickupMessage "You got the anti-personnel combat shotgun!"
	Inventory.PickupSound "misc/w_pkup"
	/*United*/ States /*of America*/ //not where I actually live, though
	{
		Spawn:
		SHOT A -1
		Stop
		
		Pickup:
		TNT1 A 0 A_JumpIfInventory("CombatShotgun", 1, "PickupFail")
		TNT1 A 0 A_JumpIfInventory("NewChaingun", 1, "PickupFail")
		TNT1 A 0 A_JumpIfInventory("NewRocketLauncher", 1, "PickupFail")
		TNT1 A 0 A_JumpIfInventory("NewSSG", 1, "PickupFail")
		TNT1 A 0 A_JumpIfInventory("NewPlasmaGun", 1, "PickupFail")
		TNT1 A 0 A_GiveInventory("CombatShotgun")
		TNT1 A 0 ACS_ExecuteAlways(655) //Switch to acquired weapon
		//Take unarmed away with heavy weapon, give it back without.
		TNT1 A 0 A_TakeInventory("PidgeonPuncher", 1)
		Stop
		
		PickupFail:
		SHOT A 1
		Fail
	}
}

Actor ShotgunPickup2: ShotgunPickup Replaces CombatShotgun {} 
//Untitled says that this only replaces the pickup, not the actual weapon. My soul hurts, this is so ugly to me

ACTOR NewShell : Ammo Replaces Shell
{
  Inventory.PickupMessage "Picked up some shotgun shells."
  Inventory.Amount 6
  Inventory.MaxAmount 24
  Ammo.BackpackAmount 6
  Ammo.BackpackMaxAmount 30
  Inventory.Icon "SHELA0"
  States
  {
  Spawn:
    SHEL A -1
    Stop
  }
}

ACTOR NewShellBox : NewShell replaces ShellBox
{
  Inventory.PickupMessage "$GOTSHELLBOX" // "Picked up a box of shotgun shells."
  Inventory.Amount 12
  States
  {
  Spawn:
    SBOX A -1
    Stop
  }
}