Actor CombatShotgun : Weapon Replaces Shotgun
//Practical shotgun: decent at everything, versatile. Doesn't pack a massive punch.
{
	Weapon.Ammotype1 "LoadedShell"	
	Weapon.Ammogive2 6
	Weapon.Ammouse1 1
	Weapon.Ammotype2 "Shell"
	Inventory.PickupMessage "You got the anti-personnel combat shotgun!"
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.NOAUTOAIM
	
	States
	{
		Ready:
			TNT1 A 0 A_JumpIfInventory("ReloadChecker", 1, "ReloadStart")
			SHTG A 1 A_WeaponReady
			Loop
		Deselect:
			TNT1 A 0 A_Lower
			SHTG A 1 A_Lower
			Loop
		Select:
			TNT1 A 0 A_TakeInventory("ReloadChecker", 1) 
			TNT1 A 0 A_Raise
			SHTG A 1 A_Raise
			Loop
		Fire: 
			TNT1 A 0 A_JumpIfNoAmmo("ReloadStart")
			SHTG A 5 A_PlaySound("weapons/shotgf", CHAN_WEAPON)
			SHTG A 0 A_FireCustomMissile("ShotgunPellet", (random(-400, 400)/100.0), 1, 0, 0, 0, (random(-300, 300)/100.0))
			SHTG A 0 A_FireCustomMissile("ShotgunPellet", (random(-400, 400)/100.0), 0, 0, 0, 0, (random(-300, 300)/100.0))
			SHTG A 0 A_FireCustomMissile("ShotgunPellet", (random(-300, 300)/100.0), 0, 0, 0, 0, (random(-200, 200)/100.0))
			SHTG A 0 A_FireCustomMissile("ShotgunPellet", (random(-300, 300)/100.0), 0, 0, 0, 0, (random(-200, 200)/100.0))
			SHTG A 0 A_FireCustomMissile("ShotgunPellet", (random(-200, 200)/100.0), 0, 0, 0, 0, (random(-100, 100)/100.0))
			SHTG A 0 A_FireCustomMissile("ShotgunPellet", (random(-200, 200)/100.0), 0, 0, 0, 0, (random(-100, 100)/100.0))
			SHTG A 0 A_FireCustomMissile("ShotgunPellet", (random(-100, 100)/100.0), 0, 0, 0, 0, 0)
			SHTG B 2
			SHTG C 10
			SHTG DC 3
			SHTG B 2
			SHTG A 5 A_ReFire
			Goto Ready
		AltFire:
			SHTG A 1 Offset(-16, 54)
			SHTG A 1 Offset(-32, 68)
			RPUN B 1 Offset(0, 32)
			RPUN C 2 A_CustomPunch(40, 1, 4, "KnockbackPuff")
			RPUN D 8
			RPUN C 7
			Goto Ready
		Flash:
			SHTF A 3 Bright A_Light1
			SHTF B 2 Bright A_Light2
			Goto LightDone
		Spawn:
			SHOT A -1
			Stop
		ReloadStart:
			TNT1 A 0 A_TakeInventory("ReloadChecker", 1)
			TNT1 A 0 A_JumpIfInventory("Shell", 1, "ReloadStart2")
			Goto ReloadFail
		ReloadStart2:
			SHTG ABC 4 A_WeaponReady(WRF_NOPRIMARY)
			Goto ReloadLoop
		ReloadLoop:
			SHTG D 5 A_WeaponReady(WRF_NOPRIMARY)
			SHTG C 7 A_WeaponReady(WRF_NOPRIMARY) //0 tic loads, 8 tics of punch readiness
			TNT1 A 0 A_TakeInventory("Shell", 1)
			TNT1 A 0 A_GiveInventory("LoadedShell", 1)
			SHTG C 0 A_PlaySound("misc/w_pkup", CHAN_WEAPON)
			SHTG C 3 A_WeaponReady
			TNT1 A 0 A_JumpIfInventory("LoadedShell", 0, "ReloadEnd")
			TNT1 A 0 A_JumpIfInventory("Shell", 1, "ReloadLoop")
		ReloadEnd:
			SHTG DCBA 4 A_WeaponReady(WRF_NOPRIMARY)
			Goto Ready
		ReloadFail:
			SHTG A 1
			Goto Ready
	}
}

Actor ShotgunPellet: PistolBullet
{
	Damage (12)
}

Actor LoadedShell : Ammo
{
	Inventory.Amount 1
	Inventory.MaxAmount 6 
	Ammo.BackpackAmount 0
	Ammo.BackPackMaxAmount 6
}