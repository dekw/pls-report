Actor PidgeonPuncher : DoomWeapon Replaces Fist
{
  Game Doom
  Obituary "$OB_MPFIST"
  Tag "$TAG_FIST"
  +WEAPON.MELEEWEAPON
  +WEAPON.NOAUTOFIRE
  +WEAPON.NOALERT
  +INVENTORY.UNDROPPABLE
  
  Weapon.Kickback 100
  States
  {
	Ready:
		PUNG A 1 A_WeaponReady
		//Use Drop Weapon on unarmed to disarm.
		TNT1 A 0 A_JumpIfInventory("DropChecker", 1, "Disarm")
		Loop
	Deselect:
		PUNG A 1 A_Lower
		PUNG A 0 A_Lower
		Loop
	Disarm:
		TNT1 A 0 A_TakeInventory("DropChecker", 1)
		PUNG B 3 Offset(0, 32)
		PUNG C 3 A_CustomPunch(30, 1, 4, "DisarmPuff", 108)
		PUNG D 3
		PUNG C 2
		TNT1 A 0
		PUNG A 6
		Goto Ready
	Select:
		TNT1 A 0 ACS_ExecuteAlways(651)
		PUNG A 1 A_Raise
		PUNG A 0 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("Combo", 2, "Doublepunch")
		TNT1 A 0 A_GiveInventory("Combo", 1)
		PUNG B 2 Offset(0, 32)
		PUNG C 1 A_CustomPunch(30, 1, 4, "MeleePuff", 92)
		PUNG D 4
		PUNG C 2
		PUNG C 1 A_WeaponReady
		PUNG AAAAAAAA 1 A_WeaponReady
		TNT1 A 0 A_TakeInventory("Combo", 2)
		Goto Ready
	AltFire:
		TNT1 A 0 A_JumpIfInventory("Combo", 2, "Punchhard")
		RPUN B 2
		RPUN C 2 A_CustomPunch(40, 1, 4, "MeleePuff", 92)
		RPUN D 5
		RPUN C 4
		TNT1 A 0 A_TakeInventory("Combo", 2)
		Goto Ready
	PunchHard:
		TNT1 A 0 A_TakeInventory("Combo", 2)
		RPUN B 2
		RPUN C 2 A_CustomPunch(90, 1, 4, "KnockbackPuff", 108)
		RPUN D 10
		RPUN C 8
		RPUN B 3
		Goto Ready
	Doublepunch:
		TNT1 A 0 A_TakeInventory("Combo", 2)
		PUNG B 2 Offset(0, 32)
		PUNG C 1 A_CustomPunch(30, 1, 4, "MeleePuff", 92)
		PUNG D 3
		PUNG C 2
		RPUN B 2
		RPUN C 2 A_CustomPunch(40, 1, 4, "KnockbackPuff", 96)
		RPUN D 5
		RPUN C 4
		Goto Ready
  }
}

Actor MeleePuff : BulletPuff //Melee hits should stun zombies and such
{
	DamageType "CustomMelee"
	AttackSound "sounds/POW"
	SeeSound "sounds/POW"
	+PUFFONACTORS
}

Actor DisarmPuff : MeleePuff
{
	//For disarming Zombies
	Damage (1)
	DamageType "Disarm"
}

Actor KnockbackPuff : MeleePuff
{
	DamageType "Knockback"
	AttackSound "sounds/WHACK"
	SeeSound "sounds/WHACK"
}
/*
* You get inventory item combo when attacking, and then have it taken if you're
* idle for long enough, or if you attack again at the right time.
* This allows for basic combos in melee (RRR and RRL are the only ones in atm)
*/



Actor Combo : Ammo //I DON'T KNOW HOW THE HELL TO MAKE IT WORK WITH INVENTORY IT JUST DOESN"T I'M SORRY
{
	Inventory.Amount 0
	Inventory.MaxAmount 2
	Ammo.BackpackAmount 0 
	Ammo.BackPackMaxAmount 2
}
