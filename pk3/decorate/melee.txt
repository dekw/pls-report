Actor PidgeonPuncher : DoomWeapon Replaces Fist
{
  Game Doom
  Obituary "$OB_MPFIST"
  Tag "$TAG_FIST"
  +WEAPON.MELEEWEAPON
  +WEAPON.NOAUTOFIRE
  //THIS AIN'T NO WIMPY WEAPON, GET OUTTA HERE FLAG
  Weapon.Kickback 100
  States
  {
	Ready:
		PUNG A 1 A_WeaponReady
		Loop
	Deselect:
		PUNG A 1 A_Lower
		Loop
	Select:
		PUNG A 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("Combo", 2, "Doublepunch")
		TNT1 A 0 A_GiveInventory("Combo", 1)
		PUNG B 2 Offset(0, 32)
		PUNG C 2 A_CustomPunch(30, 1, 4, "MeleePuff", 80)
		PUNG D 6
		PUNG C 5
		PUNG AAAAAAAA 1 A_WeaponReady
		TNT1 A 0 A_TakeInventory("Combo", 2)
		Goto Ready
	AltFire:
		TNT1 A 0 A_JumpIfInventory("Combo", 2, "Punchhard")
		RPUN B 2
		RPUN C 2 A_CustomPunch(40, 1, 4, "MeleePuff", 80)
		RPUN D 8
		RPUN C 7
		TNT1 A 0 A_TakeInventory("Combo", 2)
		Goto Ready
	PunchHard:
		TNT1 A 0 A_TakeInventory("Combo", 2)
		RPUN B 2
		RPUN C 2 A_CustomPunch(70, 1, 4, "KnockbackPuff", 90)
		RPUN D 10
		RPUN C 8
		RPUN B 3
		Goto Ready
	Doublepunch:
		TNT1 A 0 A_TakeInventory("Combo", 2)
		PUNG B 2 Offset(0, 32)
		PUNG C 2 A_CustomPunch(30, 1, 4, "MeleePuff", 80)
		PUNG D 4
		PUNG C 2
		RPUN B 2
		RPUN C 2 A_CustomPunch(40, 1, 4, "KnockbackPuff", 80)
		RPUN D 5
		RPUN C 4
		Goto Ready
  }
}

Actor MeleePuff : BulletPuff //Melee hits should stun zombies and such
{
	DamageType "CustomMelee"
	AttackSound "sounds/POW"
	SeeSound "sounds/POW"
	+PUFFONACTORS
}

Actor KnockbackPuff : MeleePuff
{
	DamageType Knockback
	AttackSound "sounds/WHACK"
	SeeSound "sounds/WHACK"
}

Actor Combo : Ammo //I DON'T KNOW HOW THE HELL TO MAKE IT WORK WITH INVENTORY IT JUST DOESN"T I'M SORRY
{
	Inventory.Amount 0
	Inventory.MaxAmount 2
	Ammo.BackpackAmount 0 
	Ammo.BackPackMaxAmount 2
}
