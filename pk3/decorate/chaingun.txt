Actor NewChaingun : Weapon Replaces Chaingun
//Lots of firepower, at cost of speed and versatility
{
	Weapon.Ammotype1 "LoadedChaingun"	
	Weapon.Ammogive2 0
	Weapon.Ammouse1 1
	Weapon.Ammotype2 "Clip"
	Inventory.PickupMessage "You got the chaingun!"
	
	+INVENTORY.UNDROPPABLE
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.NOAUTOAIM
	
	States
	{
		Ready:
			TNT1 A 0 A_JumpIfInventory("ReloadChecker", 1, "ReloadStart")
			TNT1 A 0 A_JumpIfInventory("DropChecker", 1, "Drop")
			CHGG A 1 A_WeaponReady
			Loop
		Drop:
			TNT1 A 0 A_TakeInventory("DropChecker", 1)
			CHGG A 1 ACS_ExecuteAlways(651)
			TNT1 A 0 A_GiveInventory("PidgeonPuncher", 1)
			TNT1 A 0 A_SpawnItemEx("ChaingunPickup", 64, 0, 18, 18, 0, 0, 0)
			TNT1 A 0 A_TakeInventory("DropChecker", 1)
			TNT1 A 0 A_TakeInventory("NewChaingun", 1)
			Goto Ready
		Deselect:
			CHGG A 1 A_Lower
			CHGG A 1 A_Lower
			CHGG B 1 A_Lower
			CHGG B 1 A_Lower
			Loop
		Select:
			TNT1 A 0 A_TakeInventory("ReloadChecker", 1) 
			TNT1 A 0 ACS_ExecuteAlways(652)
			CHGG A 1 A_Raise
			CHGG A 1 A_Raise
			CHGG B 1 A_Raise
			CHGG B 1 A_Raise
			Loop
		Fire:
			TNT1 A 0 A_JumpIfNoAmmo("ReloadStart")
			CHGG A 6 A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			CHGG B 6 A_PlaySound("misc/w_pkup", CHAN_WEAPON)
			CHGG B 4 A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			CHGG AB 4 A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			CHGG AB 3 A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			CHGG AB 2 A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
		Hold:
			TNT1 A 0 A_JumpIfNoAmmo("ReloadStart")
			TNT1 A 0 A_PlaySound("weapons/pistol", CHAN_WEAPON)
			CHGG A 2 A_FireCustomMissile("ChaingunBullet", (random(-300, 300)/100.0), 1, 0, 0, 0, random(-200, 200)/100.0)
			TNT1 A 0 A_PlaySound("weapons/pistol", CHAN_WEAPON)
			CHGG B 2 A_FireCustomMissile("ChaingunBullet", (random(-300, 300)/100.0), 1, 0, 0, 0, random(-200, 200)/100.0)
			CHGG B 0 A_ReFire
			Goto Ready
		AltFire:
			CHGG A 1 Offset(-16, 54)
			CHGG A 1 Offset(-24, 61)
			CHGG A 1 Offset(-32, 68)
			RPUN B 1 Offset(0, 32)
			RPUN C 2 A_CustomPunch(40, 1, 4, "KnockbackPuff")
			RPUN D 8
			RPUN C 7
			Goto Ready
		Flash:
			CHGF A 5 Bright A_Light1
			Goto LightDone
			CHGF B 5 Bright A_Light1
			Goto LightDone
		Spawn:
			MGUN A -1
			Stop
		ReloadStart:
			TNT1 A 0 A_TakeInventory("ReloadChecker", 1)
			TNT1 A 0 A_JumpIfInventory("Clip", 1, "ReloadStart2")
			Goto ReloadFail
		ReloadStart2:
			CHGG A 2 Offset(0, 32) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			CHGG B 2 Offset(0, 34) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			CHGG A 2 Offset(0, 36) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			CHGG B 2 Offset(0, 38) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			CHGG A 2 Offset(0, 40) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			CHGG B 2 Offset(0, 42) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			CHGG A 2 Offset(0, 44) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			CHGG B 2 Offset(0, 46) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			CHGG A 2 Offset(0, 48) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			CHGG B 2 Offset(0, 50) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			CHGG A 2 Offset(0, 52) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			CHGG B 2 Offset(0, 54) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			CHGG A 2 Offset(0, 56) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			CHGG B 2 Offset(0, 58) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			CHGG A 2 Offset(0, 60) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			CHGG B 2 Offset(0, 62) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			CHGG A 18 Offset(0, 64) A_PlaySound("misc/w_pkup", CHAN_WEAPON)
			Goto ReloadLoop
		ReloadLoop:
			TNT1 A 0 A_TakeInventory("Clip", 1)
			TNT1 A 0 A_GiveInventory("LoadedChaingun", 1)
			TNT1 A 0 A_JumpIfInventory("LoadedChaingun", 0, "ReloadEnd")
			TNT1 A 0 A_JumpIfInventory("Clip", 1, "ReloadLoop")
		ReloadEnd:
			CHGG B 2 Offset(0, 62) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			CHGG A 2 Offset(0, 59) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			CHGG B 2 Offset(0, 56) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			CHGG A 2 Offset(0, 53) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			CHGG B 2 Offset(0, 50) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			CHGG A 2 Offset(0, 47) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			CHGG B 2 Offset(0, 44) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			CHGG A 2 Offset(0, 41) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			CHGG B 2 Offset(0, 38) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			CHGG A 2 Offset(0, 35) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			CHGG B 2 Offset(0, 32) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
			Goto Ready
		ReloadFail:
			CHGG A 1
			Goto Ready
	}
}

Actor LoadedChaingun : Ammo
{
	Inventory.Amount 1
	Inventory.MaxAmount 64
	Ammo.BackpackAmount 0
	Ammo.BackPackMaxAmount 64
}

Actor ChaingunBullet: FastProjectile
{
	Damage (16)
	Scale 0.1
	Radius 1
	Height 1
	Speed 80
	+BLOODSPLATTER
	States
	{
		Spawn:
			MISL A 1 A_SpawnItem("CGTrailPuff")
			Loop
		Death:
			TNT1 A 1 A_SpawnItem("BulletPuff")
			stop
		XDeath:
			TNT1 A 1
			stop
	}
}

Actor CGTrailPuff : TrailPuff
{
	Scale 0.1
}

Actor ChaingunPickup: CustomInventory Replaces Chaingun
{
	Inventory.PickupMessage "You got the chaingun!"
	States
	{
		Spawn:
		MGUN A -1
		Stop
		
		Pickup:
		TNT1 A 0 A_JumpIfInventory("CombatShotgun", 1, "PickupFail")
		TNT1 A 0 A_JumpIfInventory("NewChaingun", 1, "PickupFail")
		TNT1 A 0 A_JumpIfInventory("NewRocketLauncher", 1, "PickupFail")
		TNT1 A 0 A_JumpIfInventory("NewSSG", 1, "PickupFail")
		TNT1 A 0 A_JumpIfInventory("NewPlasmaGun", 1, "PickupFail")
		TNT1 A 0 A_GiveInventory("NewChaingun")
		Stop
		
		PickupFail:
		MGUN A 1
		Fail
	}
}

Actor ChaingunPickup2: ChaingunPickup Replaces NewChaingun {} 
